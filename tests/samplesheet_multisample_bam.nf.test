nextflow_pipeline {

    name "Test pipeline GENOMICMEDICINESWEDEN_NALLO"
    script "../main.nf"
    tag "PIPELINE"

    test("samplesheet_multisample_bam | --filter_variants_hgnc_ids null --phaser hiphase") {
        tag "samplesheet_multisample_bam"

        when {
            params {
                pipelines_testdata_base_path = 'https://raw.githubusercontent.com/genomic-medicine-sweden/test-datasets/9e0f40af8fb3ab7f8a3daecaeb03a0acda1e464e/'
                input                        = "$projectDir/assets/samplesheet_multisample_bam.csv"
                outdir                       = "$outputDir"
                filter_variants_hgnc_ids     = null
                phaser                       = "hiphase"
                sv_callers                   = "severus,hificnv"
                sv_callers_to_run            = "severus,hificnv,sniffles"
            }
        }

        then {
            // stable_name: All files + folders in ${params.outdir}/ with a stable name
            def stable_name      = getAllFilesFromDir(params.outdir, relative: true, includeDir: true, ignore: ['pipeline_info/*.{html,json,txt}'])
            // stable_path: All files in ${params.outdir}/ with stable content
            def stable_path      = getAllFilesFromDir(params.outdir, ignoreFile: 'tests/.nftignore')
            // bam_files: Stable bam files
            def bam_files        = getAllFilesFromDir(params.outdir, ignoreFile: 'tests/.nftignore_unstable_alignments', include: ['**/*.bam'])
            // Reference (needed for CRAM inspection)
            def fasta = params.pipelines_testdata_base_path + 'reference/hg38.test.fa'
            // cram_files: Stable cram files
            def cram_files       = getAllFilesFromDir(params.outdir, ignoreFile: 'tests/.nftignore_unstable_alignments', include: ['**/*.cram'])
            // all_vcf_files: All vcf files - can have unstable variants and/or unstable headers
            def all_vcf_files    = getAllFilesFromDir(params.outdir, include: ['**/*.vcf.gz'])
            // stable_vcf_files: Vcf files with stable variants - can be snapshot using variantsMD5
            def stable_vcf_files = getAllFilesFromDir(params.outdir, ignoreFile: 'tests/.nftignore_unstable_variants', include: ['**/*.vcf.gz'])
            assertAll(
                { assert workflow.success},
                { assert snapshot(
                    // Number of successful tasks
                    workflow.trace.succeeded().size(),
                    // pipeline versions.yml file for multiqc from which Nextflow version is removed because we tests pipelines on multiple Nextflow versions
                    removeNextflowVersion("$outputDir/pipeline_info/nallo_pipeline_software_mqc_versions.yml"),
                    // All stable path name, with a relative path
                    stable_name,
                    // All files with stable contents
                    stable_path,
                    // All bam files
                    bam_files.collect{ file -> [ file.getName(), bam(file.toString(), stringency: 'silent').getReadsMD5() ] },
                    // All cram files
                    cram_files.collect{ file -> [ file.getName(), cram(file.toString(), fasta, stringency: 'silent').getReadsMD5() ] },
                    // All vcf files
                    all_vcf_files.collect{ file -> [ file.getName(), path(file.toString()).vcf.summary ] },
                    // Stable vcf files
                    stable_vcf_files.collect{ file -> [ file.getName(), path(file.toString()).vcf.variantsMD5 ] }
                ).match() }
            )
        }
    }

}
