/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    publishDir = [
        path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
        mode: params.publish_dir_mode,
        saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
    ]

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Process in main workflow
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    withName: '.*:NALLO:BCFTOOLS_STATS' {
        ext.prefix = { "${vcf}" }
        publishDir = [
            path: { "${params.outdir}/snvs/stats/single_sample" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:NALLO:BCFTOOLS_CONCAT' {
        ext.prefix = { params.skip_snv_annotation ? "${meta.id}_snv" : "${meta.id}_snv_annotated" }
        ext.args = { [
            '--allow-overlaps',
            '--output-type z',
            '--write-index=tbi'
        ].join(' ') }
        publishDir = [
            path: { "${params.outdir}/snvs/multi_sample/${meta.id}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : (!params.skip_rank_variants && !params.skip_snv_annotation ? null : filename) }
        ]
    }

    withName: '.*:NALLO:BCFTOOLS_PLUGINSPLIT' {
        ext.args = [
            '--output-type z',
            '--write-index=tbi'
        ].join(' ')
        publishDir = [
            path: { "${params.outdir}/snvs/single_sample/" },
            mode: params.publish_dir_mode,
            // Can't use prefix as it would come from the original file
            saveAs: { filename ->
                if (filename.equals('versions.yml')) {
                    null
                } else {
                    def matcher = filename =~ /(.+)(\.vcf\.gz(?:\.tbi)?)$/
                    def sample = matcher[0][1]
                    def extension = matcher[0][2]
                    def annotated = params.skip_snv_annotation ? "" : (params.skip_rank_variants ? "_annotated" : "_annotated_ranked" )
                    "${sample}/${sample}_snv${annotated}${extension}"
                }
            }
        ]
    }
    withName: '.*:NALLO:FASTQC' {
        ext.args = '--quiet'
        ext.prefix = { "${reads}" }
        publishDir = [
            path: { "${params.outdir}/qc_raw_reads/fastqc/${meta.id}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:NALLO:FQCRS' {
        ext.prefix = { "${reads}" }
        publishDir = [
            path: { "${params.outdir}/qc_raw_reads/fqcrs/${meta.id}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:NALLO:CAT_FASTQ' {
        publishDir = [
            enabled: false
        ]
    }

    withName: '.*:NALLO:SPLIT_BED_CHUNKS' {
        publishDir = [
            enabled: false
        ]
    }

    withName: '.*:NALLO:BUILD_INTERVALS' {
        publishDir = [
            enabled: false
        ]
    }

    withName: '.*:NALLO:FASTP' {
        ext.prefix = { "${reads.simpleName}" }
        ext.args = { [
            '--disable_adapter_trimming',
            '--disable_quality_filtering',
            "--split ${params.split_fastq}"
        ].join(' ').trim() }
        publishDir = [
            enabled: false
        ]
    }

    withName: '.*:NALLO:MINIMAP2_ALIGN' {
        ext.args = { [
            "-y",
            params.preset.equals('ONT_R10') ? "-x lr:hq" : "-x map-hifi",
            "--secondary=no",
            "-Y",
            "-R @RG\\\\tID:${meta.id}\\\\tSM:${meta.id}"
        ].join(' ') }
        publishDir = [
                mode: params.publish_dir_mode,
                path: { "${params.outdir}/aligned_reads/minimap2/${meta.id}" },
                // only a single BAM file per sample
                saveAs: { filename ->
                    filename.equals('versions.yml') ? null : (meta.n_files == 1 ? filename : null)
                }
            ]
    }

    withName: '.*:NALLO:SAMTOOLS_MERGE' {
        ext.args = '--write-index'
        publishDir = [
            path: { "${params.outdir}/aligned_reads/minimap2/${meta.id}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:NALLO:ECHTVAR_ENCODE' {
        publishDir = [
            path: { "${params.outdir}/databases/echtvar/encode/${meta.id}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Summary
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    withName: CUSTOM_DUMPSOFTWAREVERSIONS {
        publishDir = [
            path: { "${params.outdir}/pipeline_info" },
            mode: params.publish_dir_mode,
            pattern: '*_versions.yml'
        ]
    }
}
